apply plugin: 'com.android.application'
apply plugin: 'org.anarres.jarjar'
apply plugin: "com.github.johnrengelman.shadow"

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.geysermc.app.android"
        minSdkVersion 24 // This should work for earlier versions (likely 21) when Android Studio 4.1 is out
        targetSdkVersion 28
        versionCode 100005 // Format 00.00.000    EG 1.0.1 -> 01.00.001 -> 0100001 -> 100001
        versionName "1.0.5-BETA"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def newName = outputFileName

                newName = newName.replace("app-", "${variant.mergedFlavor.applicationId}-")
                newName = newName.replace("-release", "-${variant.versionName}")
                newName = newName.replace("-debug", "-${variant.versionName}-debug")

                outputFileName = newName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }

    configurations {
        nnio
    }
}

repositories {
    maven {
        url = uri('https://repo.nukkitx.com/maven-releases/')
    }

    maven {
        url = uri('https://repo.nukkitx.com/maven-snapshots/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://oss.sonatype.org/content/repositories/snapshots/')
    }

    mavenCentral()
}

task nnioJar (type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    relocate 'org.lukhnos.nnio.file', 'java.nio.file'
    relocate 'org.lukhnos.nnio.channels', 'java.nio.channels'

    configurations = [project.configurations.nnio]
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.8"

    // Remove epoll, kqueue and fastutil from the protocol
    implementation ("com.nukkitx.protocol:bedrock-v408:2.6.0-SNAPSHOT") {
        exclude group: 'com.nukkitx.fastutil', module:'fastutil-common'
        exclude group: 'com.nukkitx.network', module:'common'
        exclude group: 'io.netty', module:'netty-transport-native-epoll'
        exclude group: 'io.netty', module:'netty-transport-native-kqueue'
    }

    // Include the updated common with the android fixes
    implementation 'com.nukkitx.network:common:1.6.20'

    // Implement the cleaned fastutil manually to prevent the error on release builds
    implementation files("${buildDir}/jarjar/fastutil-cleaned.jar") { builtBy 'jarjar-repackage_fastutil-cleaned.jar' }

    // Remove the duplicate classes and include fastutil-common
    compileOnly jarjar.repackage('fastutil-cleaned.jar') {
        from 'com.nukkitx.fastutil:fastutil-common:8.3.1'

        classDelete "it.unimi.dsi.fastutil.ints.IntIterator"
        classDelete "it.unimi.dsi.fastutil.longs.LongIterator"
        classDelete "it.unimi.dsi.fastutil.objects.ObjectIterator"
        classDelete "it.unimi.dsi.fastutil.booleans.BooleanIterator"
        classDelete "it.unimi.dsi.fastutil.bytes.ByteIterator"
        classDelete "it.unimi.dsi.fastutil.doubles.DoubleIterator"
        classDelete "it.unimi.dsi.fastutil.floats.FloatIterator"
    }

    // Core Geyser
    implementation ('org.geysermc:connector:1.1.0') {
        exclude group: 'com.nukkitx.fastutil', module:'fastutil-common'
    }

    // Load a remake of nnio to support earlier Android versions
    nnio 'org.lukhnos:nnio:0.2'
    implementation tasks.nnioJar.outputs.files

    // slf4j port for Android
    implementation 'org.slf4j:slf4j-android:1.7.30'
}